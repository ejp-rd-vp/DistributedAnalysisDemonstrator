PREFIX ex: <https://example.org/>
PREFIX obo: <http://purl.obolibrary.org/obo/>
PREFIX sio: <http://semanticscience.org/resource/>
PREFIX dcterms: <http://purl.org/dc/terms/> 
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>

SELECT ?phenopacketidval ?creatorname ?createddate
WHERE {
    # Find the phenopackets that contain at least one mutation in given gene
    ?genedescr a obo:NCIT_C16612 ;
               dcterms:identifier 'VAR_GENE_ID' .
    ?vardescr sio:SIO_001403 ?genedescr .
    ?varinterpr sio:SIO_001403 ?vardescr .
    ?genomicinterp sio:SIO_001403 ?varinterpr .
    ?diagnosis sio:SIO_001403 ?genomicinterp .
    ?interpr sio:SIO_001403 ?diagnosis .
    ?phenopacket sio:SIO_001403 ?interpr ;
                 a obo:NCIT_C79269 ;
                 sio:SIO_000228 ?role .
    
    # Find phenopacket ID value
    ?phenopacketid sio:SIO_000020 ?role ;
	               sio:SIO_000300 ?phenopacketidval .
    
    # Find metadata
    ?phenopacket sio:SIO_001403 ?metadata .
    ?metadata a obo:NCIT_C52095 ;
              sio:SIO_000008 ?creator ;
              sio:SIO_000008 ?created .
    
    ?creator a dcterms:creator .
    ?creatorval sio:SIO_000628 ?creator ;
                a obo:NCIT_C70856 ;
                sio:SIO_000300 ?creatorname .
    
    ?created a dcterms:created .
    ?createdval sio:SIO_000628 ?created ;
                a obo:NCIT_C70856 ;
                sio:SIO_000300 ?createddate .
}